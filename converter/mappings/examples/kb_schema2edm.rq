# This SPARQL script converts raw Schema.org data to EDM RDF expressions
# This script is configured for the Virtuoso iqsl command line.

# Disable checkpoints while running script
checkpoint_interval(6000);

# Tell Virtuoso to prepare for a SPARQL query
SPARQL

# Prevent overflow of the transaction log
DEFINE sql:log-enable 3

# Start of the query
PREFIX schema:  <http://schema.org/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bib:     <http://bib.schema.org/>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>

INSERT INTO GRAPH <http://example.org/edm/> {
    ?uri a edm:ProvidedCHO ;
         dc:creator ?author ;
         dc:contributor ?illustrator ;
         dc:publisher ?publisher ;
         dc:title ?name ;
         dc:coverage ?about ;
         dc:description ?description ;
         dcterms:created ?created ;
         dcterms:extent ?width ;
         dcterms:extent ?height ;
         edm:preview ?thumb ;
         edm:isShownBy ?image ;
         edm:isShownAt ?url ;
         dcterms:isPartOf ?isPartOf ;
         dc:rights ?license ;
         edm:currentLocation ?spatial .
}
WHERE {
  GRAPH <http://example.org/raw/> {
	  ?uri a ?type .
    OPTIONAL {
      ?uri bib:author ?l_author .
      BIND(IF(isLiteral(?l_author),strlang(COALESCE(?l_author,""),"nl"),?l_author) as ?author)
    }
    OPTIONAL {
      ?uri schema:author ?l_author .
      BIND(IF(isLiteral(?l_author),strlang(COALESCE(?l_author,""),"nl"),?l_author) as ?author)
    }
    OPTIONAL {
      ?uri schema:author ?l_author .
      BIND(IF(isLiteral(?l_author),strlang(COALESCE(?l_author,""),"nl"),?l_author) as ?author)
    }
    OPTIONAL {
      ?uri schema:illustrator ?l_ill .
      BIND(IF(isLiteral(?l_ill),strlang(COALESCE(?l_ill,""),"nl"),?l_ill) as ?illustrator)
    }
    OPTIONAL {
      ?uri schema:publisher ?l_publisher .
      BIND(IF(isLiteral(?l_publisher),strlang(COALESCE(?l_publisher,""),"nl"),?l_publisher) as ?publisher)
    }
    OPTIONAL {
      ?uri schema:name ?l_name .
      BIND(IF(isLiteral(?l_name),strlang(COALESCE(?l_name,""),"nl"),?l_name) as ?name)
    }
    OPTIONAL {
      ?uri schema:about ?l_about .
      BIND(IF(isLiteral(?l_about),strlang(COALESCE(?l_about,""),"nl"),?l_about) as ?about)
    }
    OPTIONAL {
      ?uri schema:description ?l_desc
      BIND(IF(isLiteral(?l_desc),strlang(COALESCE(?l_desc,""),"nl"),?l_desc) as ?description)
    }
    OPTIONAL {
      ?uri schema:dateCreated ?l_created
      BIND(IF(isLiteral(?l_created),strlang(COALESCE(?l_created,""),"nl"),?l_created) as ?created)
    }
    OPTIONAL {
      ?uri schema:spatialCoverage ?l_spatial
      BIND(IF(isLiteral(?l_spatial),strlang(COALESCE(?l_spatial,""),"nl"),?l_spatial) as ?spatial)
    }
    OPTIONAL {
      ?uri schema:width ?l_width
      BIND(IF(isLiteral(?l_width),concat("width: ",?l_width),?l_width) as ?width)
    }
    OPTIONAL {
      ?uri schema:height ?l_height
      BIND(IF(isLiteral(?l_height),concat("height: ",?l_height),?l_height) as ?height)
    }
    OPTIONAL {
      ?uri schema:thumbnailUrl ?l_thumb
      BIND(IRI(replace(str(?l_thumb),"role=image&size=variable","role=thumbnail")) as ?thumb )
    }
    OPTIONAL { ?uri schema:image ?image }
    OPTIONAL { ?uri schema:url ?url }
    OPTIONAL {
      ?uri schema:isPartOf ?l_isPartOf
      BIND(IF(isLiteral(?l_isPartOf),strlang(COALESCE(?l_isPartOf,""),"nl"),?l_isPartOf) as ?isPartOf)
    }
    OPTIONAL { ?uri schema:license ?license }
  }
};

# Enable checkpoints after finshing the script
checkpoint_interval(120);
